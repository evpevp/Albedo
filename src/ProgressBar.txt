# ProgressBar
args[0] = args[0] & ! текущее значение параметра 
if args[1] = 0: args[1] = 1 & ! максимальное значение параметра 
if args[2] = 0: args[2] = 100 & ! ширина шкалы (таблицы) в процентах. По умолчанию 100% 
if $args[3] = "": $args[3]="#FF0000" & ! цвет заполненной части шкалы в формате #RRGGBB. По умолчанию - красный
if args[4] = 0: args[4] = 10 & ! высота шкалы в пикселях. по умолчанию 10.
! сначала "создаём" соответствующую таблицу 
$args['table']='<TABLE width=<<args[2]>>% border=0 cellspacing=0 cellpadding=0><TR>' 
! вычисляем ширину в процентах заполненной части 
args['x'] = (args[0] * 100) / args[1] 
! вычисляем ширину в процентах незаполненной части 
args['empty'] = 100 - args['x'] 
! Если ширина заполненной части больше нуля процентов, добавляем в таблицу ячейку с указанным цветом (красный по-умолчанию)
if args['x']>100: args['x'] = 100
if args['x']>0: 
 $args['table']+="<TD width=<<args['x']>>% bgcolor="+$args[3]+"><img height=<<args[4]>> src=""pic/blank.png""></img></TD>" 
end 
! Если ширина незаполненной части шкалы больше нуля процентов, добавляем в таблицу ячейку с другим цветом (серый). 
if args['empty']>0: 
 $args['table']+="<TD width=<<args['empty']>>% bgcolor=#888888>&nbsp;</TD>" 
end 
! "закрываем" таблицу 
$args['table']+='</TR></TABLE>' 
! результат 
$result = $args['table']
--- ProgressBar ---------------------------------

